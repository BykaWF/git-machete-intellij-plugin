import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
  id 'org.gradle.scala'
}

ideProbe()
jetbrainsAnnotations()
junit()
scala()

dependencies {
  testImplementation project(':testCommon').sourceSets.test.output
}

task syncTestResults(type: Sync) {
  from testResultsDir

  into "$buildDir/ui-test-results/${intellijVersions.uiTestsTarget}"
}

test {
  // Exclude UI tests when running `./gradlew test`,
  // unless the version of IntelliJ to use for UI tests is explicitly specified.
  def enableUiTests = intellijVersions.uiTestsTarget as boolean

  onlyIf {
    enableUiTests
  }
  if (enableUiTests) {
    dependsOn ':buildPlugin'
  }

  systemProperty 'intellij.version', intellijVersions.uiTestsTarget

  // Always re-run the tests, never assume they're up to date.
  outputs.upToDateWhen { false }

  testLogging {
    events += [TestLogEvent.STANDARD_OUT, TestLogEvent.STANDARD_ERROR]
  }

  // Let's copy the XML test results to a unique per-run directory, this makes things easier in the CI
  // where the UI tests are executed under a couple different IDE versions one after another.
  // The results in the default directory (build/test-results/) are getting overwritten which each subsequent test.
  finalizedBy(syncTestResults)
}
