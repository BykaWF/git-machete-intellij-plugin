plugins {
    id "com.diffplug.gradle.spotless" version "3.26.1"
}

allprojects {
    group 'com.virtuslab'
    version '0.0.17-SNAPSHOT'

    repositories {
        jcenter()
        mavenCentral()
    }

    apply plugin: 'java'
    sourceCompatibility = 11

    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        java {
            googleJavaFormat()
        }
    }

    ext {
        junitVersion = '4.13'
        easymockVersion = '3.1'
        powerMockVersion = '2.0.5'
        lombokVersion = '1.18.10'
        guiceVersion = '4.2.2'
        slf4jVersion = '1.7.25'
        jgitVersion = '5.5.1.201910021850-r'
        javafxVersion = '11'

        lombok = { ->
            dependencies {
                compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
                annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
            }
        }

        guice = { ->
            dependencies {
                compile group: 'com.google.inject', name: 'guice', version: guiceVersion
                compile group: 'com.google.inject.extensions', name: 'guice-assistedinject', version: guiceVersion
            }
        }

        javafx = { ->
            dependencies {
                compile group: 'org.openjfx', name: 'javafx', version: javafxVersion, ext: 'pom'
            }
        }

        slf4j = { ->
            dependencies {
                compile group: 'org.slf4j', name: 'slf4j-simple', version: slf4jVersion
            }
        }

        jgit = { ->
            slf4j()
            dependencies {
                compile group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: jgitVersion
            }
        }
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: junitVersion
        testCompile group: 'org.easymock', name: 'easymock', version: easymockVersion
        testCompile group: 'org.powermock', name: 'powermock-api-easymock', version: powerMockVersion
        testCompile group: 'org.powermock', name: 'powermock-api-mockito2', version: powerMockVersion
        testCompile group: 'org.powermock', name: 'powermock-core', version: powerMockVersion
        testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: powerMockVersion
    }
}

afterEvaluate {
    tasks.getByName('spotlessCheck').dependsOn(tasks.getByName('spotlessApply'))
}
